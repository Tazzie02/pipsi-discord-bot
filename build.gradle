buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
    }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'idea'

def versionObj = new Version(major: 0, minor: 1, revision: 0)
version = "$versionObj"
mainClassName = "net.pipsi.java.pipsidiscordbot.PipsiDiscordBot"

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': "$mainClassName"
    }
}

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.slf4j:slf4j-simple:1.7.25'

    implementation 'com.github.Tazzie02:TazbotDiscordLib:master-SNAPSHOT'
    implementation 'commons-cli:commons-cli:1.4'


    testImplementation 'junit:junit:4.12'
}

class Version {
    int major, minor, revision

    String getBuild() {
        System.getenv("TRAVIS_BUILD_NUMBER") ?: System.getProperty("TRAVIS_BUILD_NUMBER") ?: "DEV"
    }

    String toString() {
        "${major}.${minor}.${revision}_$build"
    }
}
